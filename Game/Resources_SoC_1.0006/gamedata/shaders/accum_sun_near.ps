#include "common.h"
#include "lmodel.h"

#if SUN_QUALITY > 2
#define ULTRA_SHADOWS_ON
#endif //	SUN_QUALITY>2

#ifdef ULTRA_SHADOWS_ON
#define USE_ULTRA_SHADOWS
#endif

#define SLB_SHADOW_SUN_NEAR
#include "shadow.h"

// Check Screen Space Shaders modules & addons
#include "check_screenspace.h"

#ifdef SSFX_SSS
#include "screenspace_shadows.h"
Texture2D s_ssfx_sss;
uniform float4 ssfx_issvp;
#endif

#ifdef MSAA_OPTIMIZATION
float4 main(v2p_volume I, uint iSample : SV_SAMPLEINDEX) : SV_Target
#else
float4 main(v2p_volume I) : SV_Target
#endif
{
    gbuffer_data gbd = gbuffer_load_data(GLD_P(I.tc.xy / I.tc.w, I.hpos, ISAMPLE));

    float4 _P = float4(gbd.P, gbd.mtl);
    float4 _N = float4(gbd.N, gbd.hemi);
    float4 _C = float4(gbd.C, gbd.gloss);

    // ----- light-model
    float m = _P.w;
    float4 light = plight_infinity(m, _P, _N, _C, Ldynamic_dir);

    // ----- shadow
    // SHADOWS FIXES - SSS Update 19
    // Normal Offset for biasing
    float3 NormalOffset = 0;
#ifdef SSFX_SHADOWS
    NormalOffset = _N * ssfx_shadow_bias.y * SLB_SMUL(0.25f);
#endif

    float4 P4 = float4(_P.xyz + NormalOffset, 1.0);
    float4 PS = mul(m_shadow, P4);
    float s = sunmask(P4);
    s *= shadow(PS, I.hpos.xy);

#ifdef SSFX_SSS
    s *= saturate(s_ssfx_sss.Sample(smp_nofilter, I.tc.xy / I.tc.w).b + ssfx_issvp.x);
#endif

#ifdef SSFX_ENHANCED_SHADERS // We have Enhanced Shaders installed
    return float4(SRGBToLinear(Ldynamic_color.rgb * s.xxx), 1) * light;
#else
    return float4(Ldynamic_color * light * s);
#endif
}
